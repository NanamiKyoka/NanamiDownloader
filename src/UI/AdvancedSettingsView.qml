import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import Nanami.UI 1.0

Item {
    id: root
    Layout.fillWidth: true
    Layout.fillHeight: true

    Rectangle {
        id: toast
        z: 999
        width: toastText.implicitWidth + 40
        height: 40
        radius: 6
        color: Theme.isDark ? "#333" : "#d4edda"
        border.color: Theme.isDark ? "#444" : "#c3e6cb"
        border.width: 1
        anchors.top: parent.top
        anchors.topMargin: 20
        anchors.horizontalCenter: parent.horizontalCenter
        opacity: 0
        visible: opacity > 0

        property string message: ""

        RowLayout {
            anchors.centerIn: parent
            Text {
                id: toastText
                text: toast.message
                color: Theme.isDark ? "#fff" : "#155724"
                font.pixelSize: 14
            }
        }

        Behavior on opacity { NumberAnimation { duration: 300 } }

        Timer {
            id: toastTimer
            interval: 2000
            onTriggered: toast.opacity = 0
        }

        function show(msg) {
            message = msg
            opacity = 1
            toastTimer.restart()
        }
    }

    component SettingCard: Rectangle {
        id: cardRoot
        Layout.fillWidth: true
        Layout.preferredHeight: contentCol.implicitHeight + 40
        color: Theme.surface
        radius: 8
        border.color: Theme.divider
        border.width: 1
        default property alias content: contentCol.data
        property string title: ""
        property string desc: ""
        property string feedback: ""

        ColumnLayout {
            id: contentCol
            anchors.top: parent.top; anchors.left: parent.left; anchors.right: parent.right
            anchors.margins: 20; spacing: 20

            ColumnLayout {
                Layout.fillWidth: true
                spacing: 5
                Text {
                    text: cardRoot.title
                    font.bold: true
                    font.pixelSize: 18
                    color: Theme.textPrimary
                    Layout.fillWidth: true
                    wrapMode: Text.WordWrap
                }
                Text {
                    text: cardRoot.desc
                    font.pixelSize: 13
                    color: Theme.textSecondary
                    visible: cardRoot.desc !== ""
                    Layout.fillWidth: true
                    wrapMode: Text.WordWrap
                }
            }

            Rectangle { Layout.fillWidth: true; height: 1; color: Theme.divider; visible: cardRoot.title !== "" }
        }

        Text {
            anchors.bottom: parent.bottom; anchors.left: parent.left; anchors.margins: 20; anchors.bottomMargin: 15
            visible: cardRoot.feedback !== ""
            text: cardRoot.feedback
            color: "#28a745"
            font.pixelSize: 12
            width: parent.width - 40
            wrapMode: Text.WordWrap
        }
    }

    component CustomCheckBox: CheckBox {
        id: ccb
        indicator: Rectangle {
            implicitWidth: 20
            implicitHeight: 20
            x: ccb.leftPadding
            y: parent.height / 2 - height / 2
            radius: 4
            color: ccb.checked ? Theme.accent : "transparent"
            border.color: ccb.checked ? Theme.accent : (Theme.isDark ? "#666" : "#bbb")
            border.width: 1.5
            Text {
                anchors.centerIn: parent; text: "âœ“"; font.pixelSize: 14; font.bold: true; color: "white"; visible: ccb.checked
            }
            Behavior on color { ColorAnimation { duration: 100 } }
        }
        contentItem: Text {
            text: ccb.text; font: ccb.font; color: Theme.textPrimary; verticalAlignment: Text.AlignVCenter; leftPadding: ccb.indicator.width + ccb.spacing
            wrapMode: Text.WordWrap
        }
    }

    component BlueSwitch: Switch {
        indicator: Rectangle {
            implicitWidth: 44; implicitHeight: 24; radius: 12
            color: parent.checked ? "#007bff" : (Theme.isDark ? "#444" : "#ccc")
            border.width: 0
            Rectangle {
                x: parent.parent.checked ? parent.width - width - 2 : 2
                y: 2; width: 20; height: 20; radius: 10
                color: "white"
                Behavior on x { NumberAnimation { duration: 150 } }
            }
            Behavior on color { ColorAnimation { duration: 150 } }
        }
    }

    component SpeedInputRow: ColumnLayout {
        id: speedRow
        property string label
        property string suffix
        property var value: 0
        signal valueSubmitted(int newValue)

        spacing: 8
        Text { text: label; color: Theme.textSecondary; font.pixelSize: 13; Layout.fillWidth: true; wrapMode: Text.WordWrap }
        RowLayout {
            Layout.fillWidth: true
            TextField {
                id: tf
                text: String(speedRow.value === undefined ? 0 : speedRow.value)
                Layout.preferredWidth: 240
                Layout.preferredHeight: 36
                color: Theme.textPrimary
                background: Rectangle {
                    color: Theme.isDark ? "#2b2b2b" : "#f5f5f5"
                    border.color: parent.activeFocus ? Theme.accent : Theme.divider
                    border.width: 1
                    radius: 4
                    Behavior on border.color { ColorAnimation { duration: 150 } }
                }
                selectByMouse: true
                leftPadding: 10
                validator: IntValidator { bottom: 0; top: 999999 }
                onEditingFinished: speedRow.valueSubmitted(parseInt(text))

                Binding { target: tf; property: "text"; value: String(speedRow.value === undefined ? 0 : speedRow.value); when: !tf.activeFocus }
            }
            Text { text: suffix; color: Theme.textSecondary; font.pixelSize: 13; visible: suffix !== ""; Layout.fillWidth: true; wrapMode: Text.WordWrap }
        }
    }

    component StandardSpinBoxRow: ColumnLayout {
        id: stdSpinRow
        property string label
        property string suffix
        property int value: 0
        property int from: 0
        property int to: 999999
        signal commit(int val)

        spacing: 8
        Text { text: label; color: Theme.textSecondary; font.pixelSize: 13; Layout.fillWidth: true; wrapMode: Text.WordWrap }
        RowLayout {
            Layout.fillWidth: true
            SpinBox {
                id: spin
                from: parent.from; to: parent.to
                value: stdSpinRow.value
                Layout.preferredWidth: 240
                Layout.preferredHeight: 36
                editable: true

                contentItem: TextInput {
                    text: parent.textFromValue(parent.value, parent.locale)
                    font: parent.font; color: Theme.textPrimary; selectionColor: Theme.accent; selectedTextColor: "white"
                    horizontalAlignment: Qt.AlignHCenter; verticalAlignment: Qt.AlignVCenter
                    readOnly: !parent.editable; validator: parent.validator; inputMethodHints: Qt.ImhFormattedNumbersOnly
                    onEditingFinished: spin.valueModified()
                }
                background: Rectangle {
                    color: Theme.isDark ? "#2b2b2b" : "#f5f5f5"; border.color: parent.activeFocus ? Theme.accent : Theme.divider; border.width: 1; radius: 4
                    Behavior on border.color { ColorAnimation { duration: 150 } }
                }
                up.indicator: Rectangle {
                    x: parent.width - width - 1; y: 1; width: 24; height: parent.height / 2 - 1; color: "transparent"
                    Text { text: "â–´"; color: parent.pressed ? Theme.accent : Theme.textSecondary; anchors.centerIn: parent; font.pixelSize: 10 }
                    MouseArea { anchors.fill: parent; onPressed: spin.increase(); }
                }
                down.indicator: Rectangle {
                    x: parent.width - width - 1; y: parent.height / 2; width: 24; height: parent.height / 2 - 1; color: "transparent"
                    Text { text: "â–¾"; color: parent.pressed ? Theme.accent : Theme.textSecondary; anchors.centerIn: parent; font.pixelSize: 10 }
                    MouseArea { anchors.fill: parent; onPressed: spin.decrease(); }
                }
                onValueModified: stdSpinRow.commit(value)
            }
            Text { text: suffix; color: Theme.textSecondary; font.pixelSize: 13; visible: suffix !== ""; Layout.fillWidth: true; wrapMode: Text.WordWrap }
        }
    }

    component CustomComboBoxRow: ColumnLayout {
        id: comboRow
        property string label
        property alias model: combo.model
        property alias currentIndex: combo.currentIndex
        property alias comboObj: combo
        property string suffixText: ""
        signal commit()

        spacing: 8
        Text { text: label; color: Theme.textSecondary; font.pixelSize: 13; Layout.fillWidth: true; wrapMode: Text.WordWrap }
        RowLayout {
            Layout.fillWidth: true
            ComboBox {
                id: combo
                Layout.preferredWidth: 240; Layout.preferredHeight: 36
                model: parent.model
                delegate: ItemDelegate {
                    width: combo.width
                    contentItem: Text { text: modelData; color: highlighted ? "white" : Theme.textPrimary; font: combo.font; elide: Text.ElideRight; verticalAlignment: Text.AlignVCenter }
                    background: Rectangle { color: highlighted ? Theme.accent : (Theme.isDark ? "#2b2b2b" : "#ffffff") }
                    highlighted: combo.highlightedIndex === index
                }
                indicator: Canvas {
                    x: combo.width - width - 10; y: combo.topPadding + (combo.availableHeight - height) / 2; width: 10; height: 6; contextType: "2d"
                    onPaint: { var ctx = getContext("2d"); ctx.reset(); ctx.moveTo(0, 0); ctx.lineTo(width, 0); ctx.lineTo(width / 2, height); ctx.closePath(); ctx.fillStyle = Theme.textSecondary; ctx.fill(); }
                }
                contentItem: Text { leftPadding: 10; rightPadding: combo.indicator.width + combo.spacing; text: combo.displayText; font: combo.font; color: Theme.textPrimary; verticalAlignment: Text.AlignVCenter; elide: Text.ElideRight }
                background: Rectangle { color: Theme.isDark ? "#2b2b2b" : "#f5f5f5"; border.color: combo.activeFocus ? Theme.accent : Theme.divider; border.width: 1; radius: 4; Behavior on border.color { ColorAnimation { duration: 150 } } }
                popup: Popup {
                    y: combo.height - 1; width: combo.width; implicitHeight: contentItem.implicitHeight; padding: 1
                    contentItem: ListView { clip: true; implicitHeight: contentHeight; model: combo.popup.visible ? combo.delegateModel : null; currentIndex: combo.highlightedIndex; ScrollIndicator.vertical: ScrollIndicator { } }
                    background: Rectangle { color: Theme.isDark ? "#2b2b2b" : "#ffffff"; border.color: Theme.divider; radius: 4 }
                }
                onActivated: comboRow.commit()
            }
            Text { text: suffixText; color: Theme.textSecondary; font.pixelSize: 12; visible: suffixText !== ""; Layout.fillWidth: true; wrapMode: Text.WordWrap }
        }
    }

    component NormalInputRow: ColumnLayout {
        id: normalInpRow
        property string label
        property alias text: inputField.text
        property alias placeholderText: inputField.placeholderText
        signal commit()

        spacing: 8
        Text { text: label; color: Theme.textSecondary; font.pixelSize: 13; Layout.fillWidth: true; wrapMode: Text.WordWrap }
        TextField {
            id: inputField
            Layout.fillWidth: true; Layout.preferredHeight: 36
            color: Theme.textPrimary
            background: Rectangle { color: parent.activeFocus?Theme.surface:(Theme.isDark?"#2b2b2b":"#f5f5f5"); border.color: parent.activeFocus?Theme.accent:Theme.divider; border.width: 1; radius: 4; Behavior on border.color { ColorAnimation { duration: 150 } } }
            selectByMouse: true; leftPadding: 10
            onEditingFinished: normalInpRow.commit()
            Binding { target: inputField; property: "text"; value: normalInpRow.text; when: !inputField.activeFocus }
        }
    }

    component ActionButtons: RowLayout {
        Layout.alignment: Qt.AlignRight
        Layout.fillWidth: true
        spacing: 12
        signal saveClicked()
        signal resetClicked()

        Item { Layout.fillWidth: true }

        Button {
            text: qsTr("é‡ç½®ä¸ºé»˜è®¤å€¼")
            flat: true
            Layout.preferredHeight: 36
            background: Rectangle { color: parent.hovered ? (Theme.isDark ? "#333" : "#eee") : "transparent"; border.color: Theme.divider; radius: 4; Behavior on color { ColorAnimation { duration: 150 } } }
            contentItem: Text {
                text: parent.text; color: Theme.textSecondary; horizontalAlignment: Text.AlignHCenter; verticalAlignment: Text.AlignVCenter
                fontSizeMode: Text.Fit; minimumPixelSize: 10; font.pixelSize: 13
            }
            onClicked: parent.resetClicked()
        }
        Button {
            text: qsTr("ä¿å­˜è®¾ç½®")
            Layout.preferredHeight: 36
            background: Rectangle { color: parent.down ? Qt.darker(Theme.accent, 1.1) : Theme.accent; radius: 4 }
            contentItem: Text {
                text: parent.text; color: "white"; horizontalAlignment: Text.AlignHCenter; verticalAlignment: Text.AlignVCenter
                fontSizeMode: Text.Fit; minimumPixelSize: 10; font.pixelSize: 13
            }
            onClicked: {
                Downloader.applyGlobalSettings()
                parent.saveClicked()
            }
        }
    }

    Flickable {
        anchors.fill: parent
        contentHeight: settingsCol.height + 60
        contentWidth: width
        clip: true
        boundsBehavior: Flickable.DragAndOvershootBounds
        ScrollBar.vertical: ScrollBar { policy: ScrollBar.AsNeeded }

        ColumnLayout {
            id: settingsCol
            width: Math.min(parent.width - 60, 800)
            anchors.horizontalCenter: parent.horizontalCenter
            anchors.top: parent.top; anchors.topMargin: 30; spacing: 20

            Text { text: qsTr("é«˜çº§è®¾ç½®"); font.bold: true; font.pixelSize: 24; color: Theme.textPrimary }
            Text { text: qsTr("é«˜çº§é…ç½®é€‰é¡¹"); color: Theme.textSecondary; font.pixelSize: 14; Layout.fillWidth: true; wrapMode: Text.WordWrap }

            SettingCard {
                id: speedCard
                title: qsTr("é€Ÿåº¦æ§åˆ¶")
                desc: qsTr("é…ç½®ä¸‹è½½å’Œä¸Šä¼ é€Ÿåº¦é™åˆ¶")

                SpeedInputRow { label: qsTr("å…¨å±€ä¸‹è½½é™é€Ÿ"); suffix: qsTr("KB/s (0 = æ— é™åˆ¶)"); value: Settings.globalMaxDownloadSpeed; onValueSubmitted: (v) => Settings.setGlobalMaxDownloadSpeed(v) }
                SpeedInputRow { label: qsTr("å…¨å±€ä¸Šä¼ é™é€Ÿ"); suffix: qsTr("KB/s (0 = æ— é™åˆ¶, ç”¨äº BitTorrent ç§å­ä¼ è¾“)"); value: Settings.globalMaxUploadSpeed; onValueSubmitted: (v) => Settings.setGlobalMaxUploadSpeed(v) }
                SpeedInputRow { label: qsTr("æœ€ä½é™é€Ÿ"); suffix: qsTr("KB/s (0 = ç¦ç”¨, å¦‚æœé€Ÿåº¦ä½äºæ­¤æŒç»­ 60s åˆ™æ–­å¼€è¿æ¥)"); value: Settings.minSpeedLimit; onValueSubmitted: (v) => Settings.setMinSpeedLimit(v) }

                Text { text: qsTr("ä¿®æ”¹ä¸Šè¿°å€¼ï¼Œç„¶åç‚¹å‡» \"ä¿å­˜è®¾ç½®\" åº”ç”¨ã€‚"); color: Theme.textSecondary; font.pixelSize: 12; topPadding: 10; Layout.fillWidth: true; wrapMode: Text.WordWrap }

                ActionButtons {
                    onSaveClicked: {
                        toast.show(qsTr("é€Ÿåº¦æ§åˆ¶è®¾ç½®å·²ä¿å­˜å¹¶æˆåŠŸåº”ç”¨!"))
                        speedCard.feedback = qsTr("âœ“ å·²ä¿å­˜è®¾ç½®: ä¸‹è½½=%1 KB/s, ä¸Šä¼ =%2 KB/s, æœ€ä½é€Ÿåº¦=%3 KB/s").arg(Settings.globalMaxDownloadSpeed).arg(Settings.globalMaxUploadSpeed).arg(Settings.minSpeedLimit)
                    }
                    onResetClicked: {
                        Settings.setGlobalMaxDownloadSpeed(0)
                        Settings.setGlobalMaxUploadSpeed(0)
                        Settings.setMinSpeedLimit(0)
                        toast.show(qsTr("é€Ÿåº¦è®¾ç½®å·²é‡ç½®"))
                    }
                }
            }

            SettingCard {
                title: qsTr("è¿æ¥ä¸æ€§èƒ½")
                desc: qsTr("é…ç½®è¿æ¥å‚æ•°å’Œä¸‹è½½æ€§èƒ½")

                StandardSpinBoxRow { label: qsTr("æœ€å¤§åŒæ—¶ä¸‹è½½æ•°"); suffix: qsTr("åŒæ—¶å¹¶è¡Œä¸‹è½½çš„ä»»åŠ¡æ•°"); from: 1; to: 100; value: Settings.maxConcurrentDownloads; onCommit: (v) => Settings.setMaxConcurrentDownloads(v) }
                StandardSpinBoxRow { label: qsTr("æ¯å°æœåŠ¡å™¨æœ€å¤§è¿æ¥æ•°"); suffix: qsTr("è¿æ¥è¶Šå¤š = é€Ÿåº¦è¶Šå¿«ï¼Œä½†å¯èƒ½ä¼šè¢«æœåŠ¡å™¨å±è”½"); from: 1; to: 16; value: Settings.maxConnectionPerServer; onCommit: (v) => Settings.setMaxConnectionPerServer(v) }
                StandardSpinBoxRow { label: qsTr("æ–‡ä»¶åˆ†æ®µ (åˆ†å‰²)"); suffix: qsTr("åˆ†å‰²ä¸‹è½½æ–‡ä»¶çš„æ®µæ•°"); from: 1; to: 16; value: Settings.split; onCommit: (v) => Settings.setSplit(v) }

                CustomComboBoxRow {
                    id: splitSizeCombo
                    label: qsTr("æœ€å°åˆ†å‰²å°ºå¯¸")
                    model: ["1M", "5M", "10M", "20M", "50M", "100M"]
                    currentIndex: model.indexOf(Settings.minSplitSize) !== -1 ? model.indexOf(Settings.minSplitSize) : 0
                    suffixText: qsTr("ä¸è¦åˆ†å‰²å°äºæ­¤å¤§å°çš„æ–‡ä»¶")
                    onCommit: Settings.setMinSplitSize(comboObj.displayText)
                }

                Text { text: qsTr("è°ƒæ•´ä¸Šè¿°å‚æ•°å¹¶ç‚¹å‡» \"ä¿å­˜è®¾ç½®\" åº”ç”¨ã€‚"); color: Theme.textSecondary; font.pixelSize: 12; topPadding: 10; Layout.fillWidth: true; wrapMode: Text.WordWrap }
                ActionButtons {
                    onSaveClicked: toast.show(qsTr("è¿æ¥ä¸æ€§èƒ½è®¾ç½®å·²ä¿å­˜!"))
                    onResetClicked: {
                        Settings.setMaxConcurrentDownloads(16)
                        Settings.setMaxConnectionPerServer(16)
                        Settings.setSplit(16)
                        Settings.setMinSplitSize("20M")
                        splitSizeCombo.currentIndex = 3
                        toast.show(qsTr("è¿æ¥è®¾ç½®å·²é‡ç½®"))
                    }
                }
            }

            SettingCard {
                title: qsTr("ä»£ç†è§„åˆ™")
                desc: qsTr("æ ¹æ® URL åŒ¹é…è®¾ç½®ç‰¹å®šä»£ç† (Aria2/M3U8)")

                ColumnLayout {
                    Layout.fillWidth: true
                    spacing: 8
                    Text { text: qsTr("è§„åˆ™åˆ—è¡¨ (æ¯è¡Œä¸€æ¡: åŸŸåæˆ–æ­£åˆ™|ä»£ç†åœ°å€)"); color: Theme.textSecondary; font.pixelSize: 13; Layout.fillWidth: true; wrapMode: Text.WordWrap }
                    TextArea {
                        id: proxyRulesArea
                        text: Settings.proxyRules
                        Layout.fillWidth: true
                        Layout.preferredHeight: 150
                        color: Theme.textPrimary
                        background: Rectangle {
                            color: Theme.isDark ? "#2b2b2b" : "#f5f5f5"
                            border.color: parent.activeFocus ? Theme.accent : Theme.divider
                            border.width: 1
                            radius: 4
                            Behavior on border.color { ColorAnimation { duration: 150 } }
                        }
                        selectByMouse: true
                        wrapMode: TextEdit.NoWrap
                        leftPadding: 10; topPadding: 10
                        placeholderText: "example.com|http://127.0.0.1:8888\n.*\\.google\\.com|socks5://127.0.0.1:1080"
                        onEditingFinished: Settings.setProxyRules(text)
                        Binding { target: proxyRulesArea; property: "text"; value: Settings.proxyRules; when: !proxyRulesArea.activeFocus }
                    }
                    Text { text: qsTr("æ”¯æŒ http, https, socks5 ä»£ç†ã€‚æ”¯æŒæ­£åˆ™è¡¨è¾¾å¼ã€‚"); color: Theme.textSecondary; font.pixelSize: 12; Layout.fillWidth: true; wrapMode: Text.WordWrap }
                }

                ActionButtons {
                    onSaveClicked: {
                        Settings.setProxyRules(proxyRulesArea.text)
                        toast.show(qsTr("ä»£ç†è§„åˆ™å·²ä¿å­˜!"))
                    }
                    onResetClicked: {
                        Settings.setProxyRules("")
                        toast.show(qsTr("ä»£ç†è§„åˆ™å·²æ¸…ç©º"))
                    }
                }
            }

            SettingCard {
                id: actionCard
                title: qsTr("ä¸‹è½½åæ“ä½œ")
                desc: qsTr("é…ç½®ä¸‹è½½å®Œæˆã€å¤±è´¥æˆ–å¯åŠ¨åçš„è‡ªåŠ¨æ“ä½œ")

                CustomComboBoxRow {
                    id: completeActionCombo
                    label: qsTr("å…¨éƒ¨ä¸‹è½½å®Œæˆæ—¶")
                    model: [qsTr("ä»€ä¹ˆéƒ½ä¸åš"), qsTr("æ’­æ”¾å£°éŸ³"), qsTr("å…³é—­è®¡ç®—æœº")]
                    currentIndex: Settings.onDownloadComplete
                    onCommit: Settings.setOnDownloadComplete(currentIndex)
                }
                CustomComboBoxRow {
                    id: failureActionCombo
                    label: qsTr("ä¸‹è½½å¤±è´¥æ—¶")
                    model: [qsTr("ä»€ä¹ˆéƒ½ä¸åš"), qsTr("è‡ªåŠ¨é‡è¯•")]
                    currentIndex: Settings.onDownloadFailure
                    onCommit: Settings.setOnDownloadFailure(currentIndex)
                    suffixText: qsTr("é‡è¯•æ¬¡æ•°ç”±ä¸‹æ–¹çš„ 'æœ€å¤§é‡è¯•æ¬¡æ•°' å†³å®š")
                }

                ActionButtons {
                    onSaveClicked: toast.show(qsTr("æ“ä½œè®¾ç½®å·²ä¿å­˜!"))
                    onResetClicked: {
                        Settings.setOnDownloadComplete(0)
                        Settings.setOnDownloadFailure(0)
                        completeActionCombo.currentIndex = 0
                        failureActionCombo.currentIndex = 0
                        toast.show(qsTr("æ“ä½œè®¾ç½®å·²é‡ç½®"))
                    }
                }
            }


            SettingCard {
                title: qsTr("è¶…æ—¶å’Œé‡è¯•è®¾ç½®")
                desc: qsTr("ä¸ºä¸ç¨³å®šç½‘ç»œé…ç½®è¿æ¥è¶…æ—¶å’Œé‡è¯•è¡Œä¸º")

                StandardSpinBoxRow { label: qsTr("è¶…æ—¶"); suffix: qsTr("ç§’é’Ÿ HTTP/FTP è¿æ¥å»ºç«‹åè¶…æ—¶"); value: Settings.timeout; onCommit: (v) => Settings.setTimeout(v); to: 86400 }
                StandardSpinBoxRow { label: qsTr("è¿æ¥è¶…æ—¶"); suffix: qsTr("ç§’é’Ÿ å»ºç«‹åˆå§‹è¿æ¥çš„è¶…æ—¶"); value: Settings.connectTimeout; onCommit: (v) => Settings.setConnectTimeout(v); to: 86400 }
                StandardSpinBoxRow { label: qsTr("æœ€å¤§é‡è¯•æ¬¡æ•°"); suffix: qsTr("æ¬¡ é‡è¯•æ¬¡æ•° (0 = æ— é™åˆ¶)"); value: Settings.maxTries; onCommit: (v) => Settings.setMaxTries(v); to: 9999 }
                StandardSpinBoxRow { label: qsTr("é‡è¯•ç­‰å¾…æ—¶é—´"); suffix: qsTr("ç§’é’Ÿ é‡è¯•ä¹‹é—´çš„ç­‰å¾…æ—¶é—´ (0 = ç¦ç”¨)"); value: Settings.retryWait; onCommit: (v) => Settings.setRetryWait(v); to: 3600 }

                Text { text: qsTr("ä¿®æ”¹ä¸Šè¿°å€¼ï¼Œç„¶åç‚¹å‡» \"ä¿å­˜è®¾ç½®\" åº”ç”¨ã€‚"); color: Theme.textSecondary; font.pixelSize: 12; topPadding: 10; Layout.fillWidth: true; wrapMode: Text.WordWrap }
                ActionButtons {
                    onSaveClicked: toast.show(qsTr("è¶…æ—¶è®¾ç½®å·²ä¿å­˜!"))
                    onResetClicked: {
                        Settings.setTimeout(60)
                        Settings.setConnectTimeout(60)
                        Settings.setMaxTries(5)
                        Settings.setRetryWait(0)
                        toast.show(qsTr("è¶…æ—¶è®¾ç½®å·²é‡ç½®"))
                    }
                }

                Rectangle { Layout.fillWidth: true; implicitHeight: tipText.implicitHeight + 20; color: Theme.isDark ? "#383838" : "#f9f9f9"; radius: 4; border.color: Theme.divider
                    RowLayout {
                        anchors.fill: parent; anchors.margins: 10; spacing: 5
                        Text { text: "ğŸ’¡"; font.pixelSize: 14 }
                        Text { id: tipText; text: qsTr("æç¤º: é’ˆå¯¹ä¸ç¨³å®šçš„ç½‘ç»œè¿æ¥ï¼Œå¢åŠ è¶…æ—¶å’Œé‡è¯•å€¼"); color: Theme.textPrimary; font.pixelSize: 12; Layout.fillWidth: true; wrapMode: Text.WordWrap }
                    }
                }
            }

            SettingCard {
                title: qsTr("BitTorrent é«˜çº§é€‰é¡¹")
                desc: qsTr("ä¸º BitTorrent ä¸‹è½½é…ç½® DHTã€å¯¹ç­‰è¿æ¥å’ŒåŠ å¯†")

                RowLayout {
                    Layout.fillWidth: true
                    CustomCheckBox { id: dhtCb; checked: Settings.enableDht; onToggled: Settings.setEnableDht(checked) }
                    ColumnLayout { spacing: 2; Layout.fillWidth: true; Text { text: qsTr("å¯ç”¨ DHT (å»ä¸­å¿ƒåŒ–ç½‘ç»œ)"); color: Theme.textPrimary; font.pixelSize: 14 } Text { text: qsTr("ä¸º Torrent ä¸‹è½½æ‰¾åˆ°æ›´å¤šç”¨æˆ·ã€‚è¿˜å¯å¯ç”¨ UDP Tracker æ”¯æŒã€‚"); color: Theme.textSecondary; font.pixelSize: 12; Layout.fillWidth: true; wrapMode: Text.WordWrap } }
                }
                Rectangle { Layout.fillWidth: true; height: 1; color: Theme.divider; opacity: 0.5 }
                StandardSpinBoxRow { label: qsTr("Torrent æœ€å¤§è¿æ¥æ•°"); suffix: qsTr("æ¯ä¸ª Torrent æœ€å¤§è¿æ¥æ•°é‡ (0 = æ— é™åˆ¶)"); value: Settings.btMaxPeers; onCommit: (v) => Settings.setBtMaxPeers(v) }
                Rectangle { Layout.fillWidth: true; height: 1; color: Theme.divider; opacity: 0.5 }
                RowLayout {
                    Layout.fillWidth: true
                    CustomCheckBox { id: cryptoCb; checked: Settings.btRequireCrypto; onToggled: Settings.setBtRequireCrypto(checked) }
                    ColumnLayout { spacing: 2; Layout.fillWidth: true; Text { text: qsTr("è¦æ±‚åŠ å¯†è¿æ¥"); color: Theme.textPrimary; font.pixelSize: 14 } Text { text: qsTr("åªæ¥å—åŠ å¯†çš„ BitTorrent æ¡æ‰‹ã€‚æ‹’ç»ä¼ ç»Ÿçš„æœªåŠ å¯†è¿æ¥ã€‚"); color: Theme.textSecondary; font.pixelSize: 12; Layout.fillWidth: true; wrapMode: Text.WordWrap } }
                }

                Text { text: qsTr("ä¿®æ”¹ä¸Šè¿°å€¼ï¼Œç„¶åç‚¹å‡» \"ä¿å­˜è®¾ç½®\" åº”ç”¨ã€‚"); color: Theme.textSecondary; font.pixelSize: 12; topPadding: 10; Layout.fillWidth: true; wrapMode: Text.WordWrap }
                ActionButtons {
                    onSaveClicked: toast.show(qsTr("BitTorrent è®¾ç½®å·²ä¿å­˜!"))
                    onResetClicked: {
                        Settings.setEnableDht(true)
                        Settings.setBtMaxPeers(55)
                        Settings.setBtRequireCrypto(false)
                        toast.show(qsTr("BitTorrent è®¾ç½®å·²é‡ç½®"))
                    }
                }
                Rectangle { Layout.fillWidth: true; implicitHeight: btTip.implicitHeight + 20; color: Theme.isDark ? "#383838" : "#f9f9f9"; radius: 4; border.color: Theme.divider
                    RowLayout {
                        anchors.fill: parent; anchors.margins: 10; spacing: 5
                        Text { text: "ğŸ’¡"; font.pixelSize: 14 }
                        Text { id: btTip; text: qsTr("æç¤º: å¯¹äºç§äºº Torrentï¼Œæ— è®ºæ˜¯å¦è¿›è¡Œæ­¤è®¾ç½®ï¼ŒDHT éƒ½ä¼šè‡ªåŠ¨ç¦ç”¨"); color: "#8a6d3b"; Layout.fillWidth: true; wrapMode: Text.WordWrap; font.pixelSize: 12 }
                    }
                }
            }

            SettingCard {
                title: qsTr("ç”¨æˆ·ä»£ç†ï¼ˆUAè®¾ç½®ï¼‰")
                desc: qsTr("é…ç½® HTTP/HTTPS ç”¨æˆ·ä»£ç†å­—ç¬¦ä¸²ï¼Œä»¥ä¾¿ä¸ä¸åŒæœåŠ¡å™¨å…¼å®¹")

                Component.onCompleted: {
                    if (uaCombo.currentIndex !== Settings.userAgentIndex) {
                        uaCombo.currentIndex = Settings.userAgentIndex
                        if (Settings.userAgentIndex !== 9) {
                            var ua = ""
                            if(Settings.userAgentIndex === 0) ua = "aria2/1.36.0"
                            if(Settings.userAgentIndex === 1) ua = "Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/120.0.0.0 Safari/537.36"
                            if(Settings.userAgentIndex === 2) ua = "Mozilla/5.0 (Windows NT 10.0; Win64; x64; rv:121.0) Gecko/20100101 Firefox/121.0"
                            if(Settings.userAgentIndex === 3) ua = "Mozilla/5.0 (Macintosh; Intel Mac OS X 14_1) AppleWebKit/605.1.15 (KHTML, like Gecko) Version/17.0 Safari/605.1.15"
                            if(Settings.userAgentIndex === 4) ua = "Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/120.0.0.0 Safari/537.36 Edg/120.0.0.0"
                            if(Settings.userAgentIndex === 5) ua = "Mozilla/5.0 (Linux; Android 10; K) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/120.0.0.0 Mobile Safari/537.36"
                            if(Settings.userAgentIndex === 6) ua = "Mozilla/5.0 (iPhone; CPU iPhone OS 17_1 like Mac OS X) AppleWebKit/605.1.15 (KHTML, like Gecko) Version/17.0 Mobile/15E148 Safari/604.1"
                            if(Settings.userAgentIndex === 7) ua = "Wget/1.21.3"
                            if(Settings.userAgentIndex === 8) ua = "curl/8.5.0"
                            if (ua !== "") {
                                uaTextField.text = ua
                            }
                        }
                    }
                }

                CustomComboBoxRow {
                    id: uaCombo
                    label: qsTr("é¢„è®¾ç”¨æˆ·ä»£ç†")
                    model: [qsTr("Aria2 é»˜è®¤å€¼ (aria2/1.36.0)"), qsTr("Chrome 120 (Windows)"), qsTr("Firefox 121 (Windows)"), qsTr("Safari 17 (macOS)"), qsTr("Edge 120 (Windows)"), qsTr("Chrome 120 (å®‰å“)"), qsTr("Safari (iPhone)"), qsTr("Wget 1.21"), qsTr("cURL 8.5"), qsTr("è‡ªå®šä¹‰")]
                    currentIndex: Settings.userAgentIndex
                    onCommit: {
                        Settings.setUserAgentIndex(currentIndex)
                        var ua = ""
                        if(currentIndex === 0) ua = "aria2/1.36.0"
                        if(currentIndex === 1) ua = "Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/120.0.0.0 Safari/537.36"
                        if(currentIndex === 2) ua = "Mozilla/5.0 (Windows NT 10.0; Win64; x64; rv:121.0) Gecko/20100101 Firefox/121.0"
                        if(currentIndex === 3) ua = "Mozilla/5.0 (Macintosh; Intel Mac OS X 14_1) AppleWebKit/605.1.15 (KHTML, like Gecko) Version/17.0 Safari/605.1.15"
                        if(currentIndex === 4) ua = "Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/120.0.0.0 Safari/537.36 Edg/120.0.0.0"
                        if(currentIndex === 5) ua = "Mozilla/5.0 (Linux; Android 10; K) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/120.0.0.0 Mobile Safari/537.36"
                        if(currentIndex === 6) ua = "Mozilla/5.0 (iPhone; CPU iPhone OS 17_1 like Mac OS X) AppleWebKit/605.1.15 (KHTML, like Gecko) Version/17.0 Mobile/15E148 Safari/604.1"
                        if(currentIndex === 7) ua = "Wget/1.21.3"
                        if(currentIndex === 8) ua = "curl/8.5.0"
                        if(currentIndex !== 9) {
                            Settings.setUserAgent(ua)
                            uaTextField.text = ua
                        } else {
                            uaTextField.text = Settings.userAgent
                        }
                    }
                }

                ColumnLayout {
                    spacing: 8
                    Text { text: qsTr("è‡ªå®šä¹‰ç”¨æˆ·ä»£ç†"); color: Theme.textSecondary; font.pixelSize: 13; Layout.fillWidth: true; wrapMode: Text.WordWrap }
                    TextField {
                        id: uaTextField
                        text: Settings.userAgent
                        Layout.fillWidth: true; Layout.preferredHeight: 36
                        enabled: uaCombo.currentIndex === 9
                        color: Theme.textPrimary
                        background: Rectangle {
                            color: enabled ? (parent.activeFocus?Theme.surface:(Theme.isDark?"#2b2b2b":"#f5f5f5")) : (Theme.isDark?"#333":"#e0e0e0")
                            border.color: parent.activeFocus?Theme.accent:Theme.divider; border.width: 1; radius: 4;
                            Behavior on border.color { ColorAnimation { duration: 150 } }
                        }
                        selectByMouse: true; leftPadding: 10
                        onEditingFinished: {
                            Settings.setUserAgent(text)
                            if(uaCombo.currentIndex !== 9) {
                                uaCombo.currentIndex = 9
                                Settings.setUserAgentIndex(9)
                            }
                        }
                        Binding {
                            target: uaTextField;
                            property: "text";
                            value: Settings.userAgent;
                            when: !uaTextField.activeFocus
                        }
                    }
                }

                ActionButtons {
                    onSaveClicked: toast.show(qsTr("ç”¨æˆ·ä»£ç†è®¾ç½®å·²ä¿å­˜!"))
                    onResetClicked: {
                        Settings.setUserAgentIndex(0)
                        Settings.setUserAgent("aria2/1.36.0")
                        toast.show(qsTr("ç”¨æˆ·ä»£ç†å·²é‡ç½®"))
                    }
                }
                Rectangle { Layout.fillWidth: true; implicitHeight: uaTip.implicitHeight + 20; color: Theme.isDark ? "#383838" : "#f9f9f9"; radius: 4; border.color: Theme.divider
                    RowLayout {
                        anchors.fill: parent; anchors.margins: 10; spacing: 5
                        Text { text: "ğŸ’¡"; font.pixelSize: 14 }
                        Text { id: uaTip; text: qsTr("æç¤º: æŸäº›æœåŠ¡å™¨å¯èƒ½ä¼šé˜»æ­¢ä» Aria2 ä¸‹è½½ã€‚ä½¿ç”¨æµè§ˆå™¨ç”¨æˆ·ä»£ç†å¯ç»•è¿‡é™åˆ¶ã€‚"); color: "#8a6d3b"; Layout.fillWidth: true; wrapMode: Text.WordWrap; font.pixelSize: 12 }
                    }
                }
            }

            SettingCard {
                title: qsTr("Aria2 RPC è®¾ç½®")
                desc: qsTr("é…ç½® Aria2 è¿œç¨‹ RPC ç›‘å¬ç«¯å£")

                ColumnLayout {
                    spacing: 8
                    Text { text: qsTr("RPC ç›‘å¬ç«¯å£"); color: Theme.textSecondary; font.pixelSize: 13; Layout.fillWidth: true; wrapMode: Text.WordWrap }
                    RowLayout {
                        TextField {
                            id: portField
                            text: Settings.rpcPort.toString()
                            Layout.preferredWidth: 240; Layout.preferredHeight: 36
                            color: Theme.textPrimary
                            background: Rectangle { color: Theme.isDark ? "#2b2b2b" : "#f5f5f5"; border.color: parent.activeFocus ? Theme.accent : Theme.divider; border.width: 1; radius: 4; Behavior on border.color { ColorAnimation { duration: 150 } } }
                            selectByMouse: true; leftPadding: 10
                            validator: IntValidator { bottom: 1024; top: 65535 }
                            onEditingFinished: Settings.setRpcPort(parseInt(text))
                            Binding { target: portField; property: "text"; value: String(Settings.rpcPort); when: !portField.activeFocus }
                        }
                        Text { text: "(1024-65535)"; color: Theme.textSecondary; font.pixelSize: 13 }
                    }
                }

                ColumnLayout {
                    spacing: 8
                    Text { text: qsTr("RPC å¯†é’¥"); color: Theme.textSecondary; font.pixelSize: 13; Layout.fillWidth: true; wrapMode: Text.WordWrap }
                    RowLayout {
                        Layout.fillWidth: true; spacing: 12
                        TextField {
                            id: secretField
                            text: Settings.rpcSecret
                            Layout.fillWidth: true; Layout.preferredHeight: 36
                            color: Theme.textPrimary
                            echoMode: showSecret.checked ? TextInput.Normal : TextInput.Password
                            background: Rectangle { color: Theme.isDark ? "#2b2b2b" : "#f5f5f5"; border.color: parent.activeFocus ? Theme.accent : Theme.divider; border.width: 1; radius: 4; Behavior on border.color { ColorAnimation { duration: 150 } } }
                            selectByMouse: true; leftPadding: 10
                            onEditingFinished: Settings.setRpcSecret(text)
                            Binding { target: secretField; property: "text"; value: Settings.rpcSecret; when: !secretField.activeFocus }
                        }
                        CustomCheckBox { id: showSecret; text: qsTr("æ˜¾ç¤º") }
                        Button {
                            text: qsTr("ç”Ÿæˆéšæœºå¯†é’¥"); Layout.preferredHeight: 36
                            background: Rectangle { color: parent.hovered ? (Theme.isDark ? "#333" : "#eee") : "transparent"; border.color: Theme.divider; radius: 4 }
                            contentItem: Text { text: parent.text; color: Theme.textSecondary; horizontalAlignment: Text.AlignHCenter; verticalAlignment: Text.AlignVCenter }
                            onClicked: { var c = "0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ"; var l = 32; var r = ""; for(var i=0; i<l; i++) r += c.charAt(Math.floor(Math.random()*c.length)); Settings.setRpcSecret(r) }
                        }
                    }
                }

                ActionButtons {
                    onSaveClicked: toast.show(qsTr("RPC è®¾ç½®å·²ä¿å­˜ (éœ€é‡å¯ç”Ÿæ•ˆ)!"))
                    onResetClicked: {
                        Settings.setRpcPort(16888)
                        Settings.setRpcSecret("")
                        toast.show(qsTr("RPC è®¾ç½®å·²é‡ç½®"))
                    }
                }
                Rectangle {
                    Layout.fillWidth: true;
                    implicitHeight: infoCol.implicitHeight + 24
                    color: Theme.isDark ? "#252526" : "#f0f0f0"; radius: 4
                    ColumnLayout {
                        id: infoCol
                        anchors.fill: parent; anchors.margins: 12; spacing: 6
                        RowLayout { Text { text: "â“˜"; color: "#007bff"; font.pixelSize: 16 } Text { text: qsTr("é‡è¦ä¿¡æ¯"); color: "#007bff"; font.bold: true; font.pixelSize: 13 } }
                        Text { text: qsTr("â€¢ RPC ç«¯å£ç”¨äºåº”ç”¨ç¨‹åºå’Œ Aria2 å¼•æ“ä¹‹é—´çš„é€šä¿¡\nâ€¢ é»˜è®¤ç«¯å£ä¸º 16888\nâ€¢ RPC å¯†é’¥ç”¨äºåº”ç”¨ç¨‹åºå’Œ Aria2 å¼•æ“ä¹‹é—´çš„èº«ä»½éªŒè¯\nâ€¢ é»˜è®¤å¯†é’¥ä¸ºç©º\nâ€¢ ä¸ºäº†å®‰å…¨èµ·è§ï¼Œå»ºè®®ä½¿ç”¨å¼ºéšæœºå¯†é’¥\nâ€¢ ç¡®ä¿è¯¥ç«¯å£æœªè¢«å…¶ä»–åº”ç”¨ç¨‹åºä½¿ç”¨"); color: Theme.textSecondary; font.pixelSize: 12; lineHeight: 1.4; Layout.fillWidth: true; wrapMode: Text.WordWrap }
                        RowLayout { Text { text: "âš ï¸"; font.pixelSize: 12 } Text { text: qsTr("æ›´æ”¹ç«¯å£æˆ–å¯†é’¥åå¿…é¡»é‡å¯åº”ç”¨ç¨‹åºï¼"); color: "#e6a23c"; font.bold: true; font.pixelSize: 12 } }
                        Text { text: qsTr("â€¢ é€‰æ‹© 1024 åˆ° 65535 ä¹‹é—´çš„ç«¯å£å·\nâ€¢ æ›´æ”¹ä¼šç«‹å³ä¿å­˜ï¼Œä½†åªæœ‰åœ¨é‡å¯åæ‰ä¼šç”Ÿæ•ˆ"); color: Theme.textSecondary; font.pixelSize: 12; lineHeight: 1.4; Layout.fillWidth: true; wrapMode: Text.WordWrap }
                    }
                }
            }

            SettingCard {
                title: qsTr("BitTorrent Trackers")
                desc: qsTr("ä¸º BitTorrent ä¸‹è½½é…ç½® Trackers")

                Text { text: qsTr("Trackers æº"); color: Theme.textSecondary; Layout.fillWidth: true; wrapMode: Text.WordWrap }

                Rectangle {
                    Layout.fillWidth: true
                    Layout.preferredHeight: 120
                    color: "transparent"
                    border.color: Theme.divider
                    border.width: 1
                    radius: 4

                    ScrollView {
                        anchors.fill: parent
                        clip: true
                        ScrollBar.vertical: ScrollBar { active: true; policy: ScrollBar.AsNeeded }

                        GridLayout {
                            width: parent.width
                            columns: 3
                            columnSpacing: 10
                            rowSpacing: 5

                            Repeater {
                                model: [
                                    {key: "XIU2-best-link", name: "XIU2 Best (Link)"},
                                    {key: "XIU2-best-cdn", name: "XIU2 Best (CDN)"},
                                    {key: "XIU2-all-link", name: "XIU2 All (Link)"},
                                    {key: "XIU2-all-cdn", name: "XIU2 All (CDN)"},
                                    {key: "XIU2-http-link", name: "XIU2 HTTP (Link)"},
                                    {key: "XIU2-http-cdn", name: "XIU2 HTTP (CDN)"},
                                    {key: "XIU2-nohttp-link", name: "XIU2 No-HTTP (Link)"},
                                    {key: "XIU2-nohttp-cdn", name: "XIU2 No-HTTP (CDN)"},
                                    {key: "ngosang-best-link", name: "Ngosang Best (Link)"},
                                    {key: "ngosang-best-mirror", name: "Ngosang Best (Mirror)"},
                                    {key: "ngosang-best-cdn", name: "Ngosang Best (CDN)"},
                                    {key: "ngosang-all-link", name: "Ngosang All (Link)"},
                                    {key: "ngosang-all-mirror", name: "Ngosang All (Mirror)"},
                                    {key: "ngosang-all-cdn", name: "Ngosang All (CDN)"},
                                    {key: "ngosang-all_udp-link", name: "Ngosang All UDP (Link)"},
                                    {key: "ngosang-all_udp-mirror", name: "Ngosang All UDP (Mirror)"},
                                    {key: "ngosang-all_udp-cdn", name: "Ngosang All UDP (CDN)"},
                                    {key: "ngosang-all_http-link", name: "Ngosang All HTTP (Link)"},
                                    {key: "ngosang-all_http-mirror", name: "Ngosang All HTTP (Mirror)"},
                                    {key: "ngosang-all_http-cdn", name: "Ngosang All HTTP (CDN)"},
                                    {key: "ngosang-all_https-link", name: "Ngosang All HTTPS (Link)"},
                                    {key: "ngosang-all_https-mirror", name: "Ngosang All HTTPS (Mirror)"},
                                    {key: "ngosang-all_https-cdn", name: "Ngosang All HTTPS (CDN)"},
                                ]

                                CustomCheckBox {
                                    Layout.fillWidth: true
                                    text: modelData.name
                                    checked: Settings.hasTrackerSource(modelData.key)
                                    onToggled: {
                                        if (checked) Settings.addTrackerSource(modelData.key)
                                        else Settings.removeTrackerSource(modelData.key)
                                    }
                                }
                            }
                        }
                    }
                }

                RowLayout {
                    Layout.topMargin: 10
                    Button {
                        text: qsTr("åŒæ­¥ Trackers")
                        background: Rectangle { color: "#007bff"; radius: 4 }
                        contentItem: Text { text: parent.text; color: "white"; anchors.centerIn: parent }
                        onClicked: {
                            toast.show(qsTr("æ­£åœ¨åŒæ­¥ Trackers ï¼Œè¯·ç¨å€™..."))
                            Downloader.fetchTrackers()
                        }
                    }
                    Item { Layout.fillWidth: true }
                    Text { text: qsTr("å¯ç”¨æ¯æ—¥è‡ªåŠ¨æ›´æ–°"); color: Theme.textPrimary }
                    BlueSwitch {
                        checked: Settings.autoUpdateTrackers
                        onToggled: Settings.setAutoUpdateTrackers(checked)
                    }
                }

                Text { text: qsTr("å½“å‰ Trackers åˆ—è¡¨"); color: Theme.textSecondary; Layout.fillWidth: true; wrapMode: Text.WordWrap }
                ScrollView {
                    Layout.fillWidth: true
                    Layout.preferredHeight: 150
                    clip: true
                    ScrollBar.vertical: ScrollBar { active: true; policy: ScrollBar.AsNeeded }

                    Column {
                        width: parent.width
                        Repeater {
                            model: Settings.btTrackers ? Settings.btTrackers.split(",").filter(function(item) { return item.trim() !== ""; }) : []
                            Text {
                                text: modelData ? modelData.trim() : ""
                                width: parent.width
                                color: Theme.textSecondary
                                wrapMode: Text.Wrap
                                font.pixelSize: 12
                            }
                        }
                    }
                }
            }

            SettingCard {
                title: qsTr("å…¶ä»–")
                desc: qsTr("å…¶ä»–æ‚é¡¹è®¾ç½®")

                RowLayout {
                    Layout.fillWidth: true
                    Text {
                        text: qsTr("å¯ç”¨ç½‘ç›˜æŒ‚è½½")
                        color: Theme.textPrimary
                        font.pixelSize: 13
                        Layout.fillWidth: true
                    }
                    BlueSwitch {
                        checked: Settings.enableCloudMount
                        onToggled: Settings.setEnableCloudMount(checked)
                    }
                }
            }
        }
    }
}